//Beginning Info:
BACKUP ~TICLove/Backup~
AUTHOR ~Tibicina~
VERSION ~1.0~
README ~TICLove/TICLove_Readme.txt~

//Crossplatform var stuff
ALWAYS
ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  /*The player is using version BG:EE*/
  PRINT ~BGEE install detected~
  INCLUDE ~TICLove/lib/liam_bgee_vars.tpa~
END ELSE BEGIN
ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
    /*The player is using version Tutu*/
    PRINT ~Tutu install detected.~
    INCLUDE ~TICLove/lib/g3_tutu_cpmvars.tpa~
END ELSE BEGIN
ACTION_IF GAME_IS ~bgt~ THEN BEGIN
        /*The player is using version BGT*/
        PRINT ~BGT install detected.~
        INCLUDE ~TICLove/lib/g3_bgt_cpmvars.tpa~
        /*The player is not using an acceptable version*/
END ELSE BEGIN FAIL ~Please install on BGEE, Tutu or BGT.~
END

DEFINE_ACTION_FUNCTION extend_area_script
STR_VAR area=""
top=""
bottom=""
BEGIN

// find the area script from the area file
COPY_EXISTING "%area%.are" override
READ_ASCII 0x94 script
BUT_ONLY

// if it doesn't exist, give it a default name
ACTION_IF "%script%" STRING_EQUAL "" BEGIN
OUTER_INNER_PATCH_SAVE script "%area%" BEGIN
REPLACE_TEXTUALLY "FW" "_AR" // TUTU naming conventions
END
END

// extend the script
ACTION_IF "%top%" STRING_COMPARE "" BEGIN
EXTEND_TOP "%script%.bcs" "%top%" EVALUATE_BUFFER
END
ACTION_IF "%bottom%" STRING_COMPARE "" BEGIN
EXTEND_BOTTOM "%script%.bcs" "%bottom%" EVALUATE_BUFFER
END
END

 /* Face orientation for CreateCreature([xxx.yyy]%FACE_x%) */
    OUTER_SPRINT ~FACE_0~ ~,0~
    OUTER_SPRINT ~FACE_1~ ~,1~
    OUTER_SPRINT ~FACE_2~ ~,2~
    OUTER_SPRINT ~FACE_3~ ~,3~
    OUTER_SPRINT ~FACE_4~ ~,4~
    OUTER_SPRINT ~FACE_5~ ~,5~
    OUTER_SPRINT ~FACE_6~ ~,6~
    OUTER_SPRINT ~FACE_7~ ~,7~
    OUTER_SPRINT ~FACE_8~ ~,8~
    OUTER_SPRINT ~FACE_9~ ~,9~
    OUTER_SPRINT ~FACE_10~ ~,10~
    OUTER_SPRINT ~FACE_11~ ~,11~
    OUTER_SPRINT ~FACE_12~ ~,12~
    OUTER_SPRINT ~FACE_13~ ~,13~
    OUTER_SPRINT ~FACE_14~ ~,14~
    OUTER_SPRINT ~FACE_15~ ~,15~
    OUTER_SPRINT ~FACE_16~ ~,16~

// Fixing Imoen's dialogue file
  /* Tutu: Giving Imoen a banter file entry in the interdi.2da */
  ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
    APPEND ~interdia.2da~ ~IMOEN                    _BIMOEN~ UNLESS ~_\(BIMOEN\|bimoen\)~
  END
  /* BGT: Giving Imoen a banter file if the Fixpack isn't installed */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~cdbehbla.pro~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
      COPY_EXISTING ~interdia.2da~ ~override~
        SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
      BUT_ONLY_IF_IT_CHANGES
    END ELSE BEGIN
        APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
        UNLESS ~BIMOEN2~
    END
  END

  /* Imoen BIMOEN.DLG rebuild */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~_bimoen.dlg~) AND (FILE_EXISTS_IN_GAME ~FW0100.are~) THEN BEGIN
    COMPILE ~TICLove/dialogues/x#bimoen_tutu.d~
  END ELSE BEGIN
    ACTION_IF NOT (FILE_EXISTS_IN_GAME ~bimoen2.dlg~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
      COMPILE ~TICLove/dialogues/x#bimoen_bgt.d~
    END
  END

  /* ACTION.IDS and TRIGGER.IDS patching to ToB - thanks, Cam, if you read it */
  /* and GTIMES.IDS, ANIMATE.IDS patching to ToB, courtesy of the BG2 Fix Pack */
  INCLUDE ~TICLove/lib/ids_entries_cleanup.tph~

/* Tutu Area Script Assignment Patching: All Areas Script ID'd */
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  INCLUDE ~TICLove/lib/tutu_area_script_assign.tph~
END

/* Area Type Flagging */
/* ToSC only: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW1500.are~) OR (FILE_EXISTS_IN_GAME ~ARW500.are~) THEN BEGIN // if TotSC is installed
  COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                ~%IsleofBalduranS%.are~ ~override~
                ~%DurlagsTower%.are~ ~override~
                ~%Farmlands%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
  BUT_ONLY_IF_IT_CHANGES
END

  /* FOREST and OUTDOOR: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW0100.are~) OR (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
  COPY_EXISTING ~%FishingVillage%.are~ ~override~
                ~%Peldvale%.are~ ~override~
                ~%LionsWay%.are~ ~override~
                ~%CoastWay%.are~ ~override~
                ~%Larswood%.are~ ~override~
                ~%ShipwrecksCoast%.are~ ~override~
                ~%HighHedge%.are~ ~override~
                ~%MutaminsGarden%.are~ ~override~
                ~%Lighthouse%.are~ ~override~
                ~%RedCanyons%.are~ ~override~
                ~%SouthBeregostRoad%.are~ ~override~
                ~%Ulcaster%.are~ ~override~
                ~%ArchaeologicalSite%.are~ ~override~
                ~%FishermansLake%.are~ ~override~
                ~%NorthNashkelRoad%.are~ ~override~
                ~%LonelyPeaks%.are~ ~override~
                ~%FirewineBridge%.are~ ~override~
                ~%BearRiver%.are~ ~override~
                ~%ValleyoftheTombs%.are~ ~override~
                ~%DryadFalls%.are~ ~override~
                ~%FireLeafForest%.are~ ~override~
                ~%GibberlingMountains%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
    BUT_ONLY_IF_IT_CHANGES

/* OUTDOOR ONLY: Tutu and BGT */
COPY_EXISTING ~%GnollStronghold%.are~ ~override~
              ~%NashkelMines%.are~ ~override~
              ~%FriendlyArmInn%.are~ ~override~
              ~%Temple%.are~ ~override~
              ~%NashkelCarnival%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00000001")
  BUT_ONLY_IF_IT_CHANGES

/* CITY and OUTDOOR */
COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
              ~%Candlekeep_Ch6%.are~ ~override~
              ~%Gullykin%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00001001")
  BUT_ONLY_IF_IT_CHANGES
END
END
END
END
//Component: Begin Component
BEGIN ~TICLove~

// Adds custom IsValidForPartyDialogue state
APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

//Adding music
COPY_EXISTING ~songlist.2da~ ~override~
SET_2DA_ENTRY 2 1 2 ~Blank~
SET_2DA_ENTRY 2 2 2 ~Blank.mus~

//Copying audio files
COPY ~TICLove/Audio~ ~override~

//TIDISPER, TICORDEL and iron11: Dispero, Cordelia and chapel guard

COPY ~TICLove/creatures/tidisper.cre~ ~override~  //probably will turn into bg1re/.. later
  SAY INITIAL_MEETING ~I'm feeling nice, so I'll kill you quickly!~ [REDALT9]
  SAY SELECT_COMMON1 ~I'm feeling nice, so I'll kill you quickly!~ [REDALT9]
  SAY NAME1 ~Dispero~
  SAY NAME2 ~Dispero~
  WRITE_ASCII 0x268 ~SHOUTDL2~ #8  // default script
  WRITE_ASCII 0x280 ~TIDISPER~ #32  //DV
  WRITE_ASCII 0x2cc ~TIDISPER~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~MFIGHT3S~ #8   //small portrait
  WRITE_BYTE 0x2c 00 // Metal color
  WRITE_BYTE 0x2d 18 // Minor color
  WRITE_BYTE 0x2e 00 // Major color
  WRITE_BYTE 0x2f 12 // Skin color
  WRITE_BYTE 0x30 16 // Leather color
  WRITE_BYTE 0x31 00 // Armor color
  WRITE_BYTE 0x32 08 // Hair color

COPY ~TICLove/creatures/ticordel.cre~ ~override~  //probably will turn into bg1re/.. later
  SAY INITIAL_MEETING ~This is ridiculous!~ [REDALT3]
  SAY SELECT_COMMON1 ~This is ridiculous!~ [REDALT3]
  SAY NAME1 ~Cordelia~
  SAY NAME2 ~Cordelia~
  WRITE_ASCII 0x268 ~SHOUTDL2~ #8  // default script
  WRITE_ASCII 0x280 ~TICORDEL~ #32  //DV
  WRITE_ASCII 0x2cc ~TICORDEL~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~FELF1S~ #8   //small portrait
  WRITE_BYTE 0x2c 05 // Metal color
  WRITE_BYTE 0x2d 03 // Minor color
  WRITE_BYTE 0x2e 63 // Major color
  WRITE_BYTE 0x2f 12 // Skin color
  WRITE_BYTE 0x30 16 // Leather color
  WRITE_BYTE 0x31 63 // Armor color
  WRITE_BYTE 0x32 03 // Hair color

COPY ~TICLove/creatures/iron11.cre~ ~override~  //probably will turn into bg1re/.. later
  SAY NAME1 ~Guard~
  SAY NAME2 ~Guard~
  WRITE_ASCII 0x248 ~SHOUT~ #8  // override script
  WRITE_ASCII 0x250 ~INITDLG~ #8  // class script
  WRITE_ASCII 0x280 ~iron11~ #32  //DV
  WRITE_ASCII 0x2cc ~iron11~ #8   //pre-joining dialogue file

//Compiling files

COMPILE EVALUATE_BUFFER ~TICLove/dialogues/TICLove.d~

//Component: This is adding our area files to the game

LAF extend_area_script STR_VAR area="%CloakwoodMines_L3%" top="TICLove/scripts/AR8602.baf" END

// Copying custom items and spells

// 1.
COPY ~TICLove/items/TICORING.itm~ ~override/TICORING.itm~
  SAY NAME1 ~Ring~
  SAY NAME2 ~Cordelia's Engagement Ring~
  SAY UNIDENTIFIED_DESC~A ring is a small band of precious metal that is used as jewelry. Some rare rings have had enchantments placed upon them that confer various magical abilities. These rings are highly sought after, especially by those of the adventuring profession.~
  SAY IDENTIFIED_DESC~A ring is a small band of precious metal that is used as jewelry. Some rare rings have had enchantments placed upon them that confer various magical abilities. These rings are highly sought after, especially by those of the adventuring profession. This ring has the effect of increasing beauty.

STATISTICS:

Equipped Abilities:
 +1 to Charisma

Not Usable By:
 Wizard Slayer~

//Component: Copying images

ACTION_IF (GAME_IS ~tutu tutu_totsc~) OR (GAME_IS ~bgt~) THEN BEGIN
  COPY ~TICLove/images~ ~override~
END ELSE BEGIN ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  COPY ~TICLove/images/bgee~ ~override~
END
END